! Alphabet definition
define HardVowel а | э | ы | о | у;
define SoftVowel я | е | и | ё | ю;
define Vowel HardVowel | SoftVowel | й;
define Sibilant ж | ч | ш | щ;
define Velar г | к | х;
define OtherCons б | в | д | з | л | м | н | п | р | с | т | ф | ц;
define Cons Sibilant | Velar | OtherCons;
define Sign ъ | ь;
define Alphabet Cons | Vowel | Sign | "-";

! Load lexicons
read lexc prepositions.lexc
define PrpLexicon

read lexc conjunctions.lexc
define ConjLexicon

define GuessNoun Alphabet+ Cons ["^GuessedRoot^":0];
read lexc nouns.lexc
substitute defined GuessNoun for "^GuessNoun"
define NounLexicon

define GuessVerb Alphabet+ Cons ["^GuessedRoot^":0];
read lexc verbs.lexc
substitute defined GuessVerb for "^GuessVerb"
define VerbLexicon
define ReflEnding с я -> с ь || Vowel "^" _;

define GuessAdverb Alphabet+ Cons ["^GuessedRoot^":0];
read lexc adverbs.lexc
substitute defined GuessAdverb for "^GuessAdverb"
define AdvLexicon

define GuessAdjective Alphabet+ Cons ["^GuessedRoot^":0];
read lexc adjectives.lexc
substitute defined GuessAdjective for "^GuessAdjective"
define AdjLexicon

define Spelling (ы -> и || [Velar | Sibilant] "^" _)
            !.o. (о -> е || [Sibilant | с] "^" _) ! only when unstressed
            .o. (ю -> у || [Velar | Sibilant | с] "^" _)
            .o. (я -> а || [Velar | Sibilant | с] "^" _);

! Morpheme boundary
define Cleanup "^" -> 0;

! Final composition
define Grammar ( (PrpLexicon
        | ConjLexicon
        | NounLexicon
        | (VerbLexicon .o. ReflEnding)
        | AdjLexicon
        | AdvLexicon)
        .o. Spelling
        .o. Cleanup);

define Attested ~$["^GuessedRoot^"] .o. Grammar;
define Guessed   $["^GuessedRoot^"] .o. Grammar;
regex Attested .p. Guessed;
