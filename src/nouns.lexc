Multichar_Symbols
+N
+m +f +n
+nom +gen +acc +dat +ins +loc
+sg +pl
^GuessNoun

LEXICON Root
! Ia
! Hard (0)
завод        MascHardIa;
^GuessNoun   MascHardIa;
! Soft (-ь/-?й)
автомобил    MascSoftIa; ! N-ь A-ь G-я D-ю I-ем L-е / N-и A-и G-ей D-ям I-ями L-ях
! представитель, уровень, гость, день, уровень, кабель, апрель
! TODO трамв[ай], случай, край
! TODO муз[ей]
! TODO алюмин[ий], кальций, мораторий
^GuessNoun   MascSoftIa;

! Ib
! Hard (-о)
лиц          NeutHardIb;
письм        NeutHardIb; ! mobile vowel
^GuessNoun   NeutHardIb;
! Soft (-е, -ие)
мор          NeutSoftIbe; ! N-е A-е G-я D-ю I-ем [L-е] / N-я A-я [G-ей] D-ям I-ями L-ях
^GuessNoun   NeutSoftIbe;
здан         NeutSoftIbie; ! N-ие A-ие G-ия D-ию I-ием [L-ии] / N-ия A-ия [G-ий] D-иям I-иями L-иях
^GuessNoun   NeutSoftIbie;

! II
! Hard (-а)
жен          FemHardII;
^GuessNoun   FemHardII;
! Soft (-я, -ия)
недел        FemSoftIIja; ! N-я A-ю G-и [D-е] I-ей [L-е] / N-и A-и [G-ь] D-ям I-ями L-ях
^GuessNoun   FemSoftIIja;
лин          FemSoftIIija; ! N-ия A-ию G-ии [D-ии] I-ией [L-ии] / N-ии A-ии [G-ий] D-иям I-иями L-иях
^GuessNoun   FemSoftIIija;

! III
новост       FemIII;
тетрад       FemIII;
^GuessNoun   FemIII;

LEXICON MascHardIa
+N+m:0 DeclensionHardIa;

LEXICON MascSoftIa
ь+N+m:0 DeclensionSoftIa;

LEXICON NeutHardIb
о+N+n:0 DeclensionHardIb;

LEXICON NeutSoftIbe
е+N+n:0 DeclensionSoftIb;
е+N+n+loc+sg:^е    #;
е+N+n+gen+pl:^ей   #;

LEXICON NeutSoftIbie
ие+N+n:и DeclensionSoftIb;
ие+N+n+loc+sg:^ии   #;
ие+N+n+gen+pl:^ий   #;

LEXICON FemHardII
а+N+f:0 DeclensionHardII;

LEXICON FemSoftIIja
я+N+f:0 DeclensionSoftII;
я+N+f+dat+sg:^е    #;
я+N+f+loc+sg:^е    #;
я+N+f+gen+pl:^ь    #;

LEXICON FemSoftIIija
ия+N+f:и DeclensionSoftII;
ия+N+f+dat+sg:^ии  #;
ия+N+f+loc+sg:^ии  #;
ия+N+f+gen+pl:^ий  #;

LEXICON FemIII
ь+N+f:0 DeclensionIII;

LEXICON DeclensionHardIa
! sg
+nom+sg:^     #;
+acc+sg:^     #; ! inanimate
DeclensionHardI
! pl
+nom+pl:^ы    #;
+acc+pl:^ы    #; ! inanimate
+gen+pl:^ов   #;
DeclensionHardI;

LEXICON DeclensionSoftIa
! sg
+nom+sg:^ь    #;
+acc+sg:^ь    #; ! inanimate
+loc+sg:^е    #;
!pl
+nom+pl:^и    #;
+acc+pl:^и    #; ! inanimate
+gen+pl:^ей   #;
DeclensionSoftI;

LEXICON DeclensionHardIb
! sg
+nom+sg:^о    #;
+acc+sg:^о    #;
! pl
+nom+pl:^а    #;
+acc+pl:^а    #; ! inanimate
+gen+pl:^     #;
DeclensionHardI;

LEXICON DeclensionSoftIb
! sg
+nom+sg:^е    #;
+acc+sg:^е    #;
! special +loc+sg
! pl
+nom+pl:^я     #;
+acc+pl:^я    #; ! inanimate
! special +gen+pl
DeclensionSoftI;

LEXICON DeclensionHardI
! sg
+gen+sg:^а    #;
+dat+sg:^у    #;
+ins+sg:^ом   #;
+loc+sg:^е    #;
!pl
+dat+pl:^ам   #;
+ins+pl:^ами  #;
+loc+pl:^ах   #;

LEXICON DeclensionSoftI
! sg
+gen+sg:^я    #;
+dat+sg:^ю    #;
+ins+sg:^ем   #;
!pl
+dat+pl:^ям   #;
+ins+pl:^ями  #;
+loc+pl:^ях   #;

LEXICON DeclensionHardII
! sg
+nom+sg:^а    #;
+acc+sg:^у    #;
+gen+sg:^ы    #;
+dat+sg:^е    #;
+ins+sg:^ой   #;
+loc+sg:^е    #;
!pl
+nom+pl:^ы    #;
+acc+pl:^ы    #; ! inanimate
+gen+pl:^     #;
+dat+pl:^ам   #;
+ins+pl:^ами  #;
+loc+pl:^ах   #;

LEXICON DeclensionSoftII
! sg
+nom+sg:^я    #;
+acc+sg:^ю    #;
+gen+sg:^и    #;
! special +dat+sg
+ins+sg:^ей   #;
! special +loc+sg
!pl
+nom+pl:^и    #;
+acc+pl:^и    #; ! inanimate
! special +gen+pl
+dat+pl:^ям   #;
+ins+pl:^ями  #;
+loc+pl:^ях   #;

LEXICON DeclensionIII
!sg
+nom+sg:^ь    #;
+acc+sg:^ь    #;
+gen+sg:^и    #;
+dat+sg:^и    #;
+ins+sg:^ью   #;
+loc+sg:^и    #;
!pl
+nom+pl:^и    #;
+acc+pl:^и    #; ! inanimate
+gen+pl:^ей   #;
+dat+pl:^ям   #;
+ins+pl:^ями  #;
+loc+pl:^ях   #;
